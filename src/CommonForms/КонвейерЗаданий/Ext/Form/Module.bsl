
// Copyright 2019 Tsukanov Alexander. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

&НаКлиенте
Перем ДанныеОчередногоОбработчикаЗадания;

&НаКлиенте
Перем ИдентификаторыЗаданий;

&НаКлиенте
Функция ОбработчикЗадания(Задание, СледующийОбработчикЗадания, ОбработчикОшибок) Экспорт
	
	ОбработчикЗадания = КонвейерЗаданийСлужебныйКлиент.ОбработчикЗадания(Задание, СледующийОбработчикЗадания, ОбработчикОшибок);
		
	_ВыполнитьОбработчикЗадания = Новый ОписаниеОповещения(
		"_ВыполнитьОбработчикЗадания",
		ЭтотОбъект,
		ОбработчикЗадания.ДополнительныеПараметры
	);
	
	Возврат _ВыполнитьОбработчикЗадания;
	
КонецФункции

&НаКлиенте
Процедура ПодготовитьОтладочныйКонтекстЛинии(Линия) Экспорт
	
	ОчереднойОбработчикЗадания = Линия.ОбработчикПервогоЗадания;
	
	Этапы = ЭтотОбъект.ЭтапыКонвейера.ПолучитьЭлементы();
	Этапы.Очистить();
	
	Стек = Новый Массив;
	
	Пока ОчереднойОбработчикЗадания <> Неопределено Цикл
		
		ПараметрыОбработчикаЗадания = ОчереднойОбработчикЗадания.ДополнительныеПараметры;
		
		НовыйЭтап = Этапы.Добавить();
		НовыйЭтап.Описание = ПараметрыОбработчикаЗадания.Задание.ИмяПроцедуры;
		НовыйЭтап.ИдентификаторЗадания = Строка(Новый УникальныйИдентификатор);
		
		ИдентификаторыЗаданий[ПараметрыОбработчикаЗадания.Задание] = НовыйЭтап.ИдентификаторЗадания;
		
		ОчереднойОбработчикЗадания = ПараметрыОбработчикаЗадания.СледующийОбработчикЗадания;
		
		// TODO: формирование дерева в соответствии со структурой заданий-операторов
		
	КонецЦикла; 
		
КонецПроцедуры

&НаКлиенте
Процедура _ВыполнитьОбработчикЗадания(КонтекстЗадания, ПараметрыОбработчикаЗадания) Экспорт
	
	ДанныеОчередногоОбработчикаЗадания.КонтекстЗадания = КонтекстЗадания;
	ДанныеОчередногоОбработчикаЗадания.ПараметрыОбработчикаЗадания = ПараметрыОбработчикаЗадания;
	
	ЭтотОбъект.ИдентификаторТекущегоЗадания = ИдентификаторыЗаданий[ДанныеОчередногоОбработчикаЗадания.ПараметрыОбработчикаЗадания.Задание];
	
	// TODO: вызов для автовыполнения до точки останова
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаШагнуть(Команда)
	
	ЭтотОбъект.ИдентификаторТекущегоЗадания = "";
	
	КонвейерЗаданийСлужебныйКлиент._ВыполнитьОбработчикЗадания(
		ДанныеОчередногоОбработчикаЗадания.КонтекстЗадания,
		ДанныеОчередногоОбработчикаЗадания.ПараметрыОбработчикаЗадания
	);
	
КонецПроцедуры

#Если Клиент Тогда
	
	ДанныеОчередногоОбработчикаЗадания = Новый Структура;
	ДанныеОчередногоОбработчикаЗадания.Вставить("КонтекстЗадания");
	ДанныеОчередногоОбработчикаЗадания.Вставить("ПараметрыОбработчикаЗадания");
	
	ИдентификаторыЗаданий = Новый Соответствие;
	
#КонецЕсли