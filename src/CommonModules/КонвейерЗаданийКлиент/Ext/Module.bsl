
Функция СоздатьЛинию() Экспорт
	
	Линия = Новый Структура;
	Линия.Вставить("Задания", Новый Массив);
	Линия.Вставить("ОбработчикЗадания", Неопределено);
	
	Возврат Линия;
	
КонецФункции 

Процедура Пуск(Линия, ОбработчикОшибок, СледующийОбработчикЗадания, ОбщиеПараметры, ИмяВызывающейПроцедуры) Экспорт
	
	Линия.ОбработчикЗадания = ПостроитьКонвейернуюЛинию(Линия.Задания, ОбработчикОшибок, СледующийОбработчикЗадания);
	
	Вызвать(Линия.ОбработчикЗадания, ОбщиеПараметры, ИмяВызывающейПроцедуры);
	
КонецПроцедуры

Процедура Вызвать(ОбработчикЗадания, ОбщиеПараметры, ИмяВызывающейПроцедуры) Экспорт
	
	КонтекстЗадания = Новый Структура;
	КонтекстЗадания.Вставить("ИмяВызывающейПроцедуры", ИмяВызывающейПроцедуры);
	КонтекстЗадания.Вставить("ОбщиеПараметры", ОбщиеПараметры);
	КонтекстЗадания.Вставить("СледующийОбработчикЗадания", Неопределено);
	
	ВыполнитьОбработкуОповещения(ОбработчикЗадания, КонтекстЗадания);
	
КонецПроцедуры

Процедура _ВыполнитьОбработчикЗадания(КонтекстЗадания, ДополнительныеПараметры) Экспорт
	
	Попытка
		
		Инвариант(КонтекстЗадания.ИмяВызывающейПроцедуры <> Неопределено); 
		
		Сообщить(СтрШаблон("Вызывающий(%1) -> Задание(%2)", КонтекстЗадания.ИмяВызывающейПроцедуры, ДополнительныеПараметры.Задание.ИмяПроцедуры));
		
		КонтекстЗадания.СледующийОбработчикЗадания = ДополнительныеПараметры.СледующийОбработчикЗадания;
		
		ФиксированныйКонтекстЗадания = Новый ФиксированнаяСтруктура(КонтекстЗадания);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Задание, ФиксированныйКонтекстЗадания);
		
	Исключение
		
		Если ДополнительныеПараметры.ОбработчикОшибок <> Неопределено Тогда 
		
			КонтекстОшибки = КонтекстОшибки(ИнформацияОбОшибке(), КонтекстЗадания.ОбщиеПараметры);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикОшибок, КонтекстОшибки);
			
		КонецЕсли; 
		
	КонецПопытки;
	
КонецПроцедуры

Процедура _ОбработатьОшибку(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	If ДополнительныеПараметры._ОбработчикОшибок <> Неопределено Тогда
		
		КонтекстОшибки = КонтекстОшибки(ИнформацияОбОшибке, ДополнительныеПараметры._ОбщиеПараметры);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры._ОбработчикОшибок, КонтекстОшибки);
			
		СтандартнаяОбработка = Ложь;
	
	EndIf; 
	
КонецПроцедуры 

Функция КонтекстОшибки(ИнформацияОбОшибке, ОбщиеПараметры)
	
	КонтекстОшибки = Новый Структура;
	КонтекстОшибки.Вставить("ИнформацияОбОшибке", ИнформацияОбОшибке);
	КонтекстОшибки.Вставить("ОбщиеПараметры", ОбщиеПараметры);
	
	Возврат КонтекстОшибки;
	
КонецФункции

Функция ОбработчикЗадания(Задание, СледующийОбработчикЗадания, ОбработчикОшибок)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Задание", Задание);
	ДополнительныеПараметры.Вставить("СледующийОбработчикЗадания", СледующийОбработчикЗадания);
	ДополнительныеПараметры.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	_ВыполнитьОбработчикЗадания = Новый ОписаниеОповещения(
		"_ВыполнитьОбработчикЗадания",
		ЭтотОбъект,
		ДополнительныеПараметры
	);
	
	Возврат _ВыполнитьОбработчикЗадания;
	
КонецФункции 

// Создает новый обработчик ошибок
Функция ОбработчикОшибок(ИмяПроцедуры, Модуль) Экспорт
	
	Возврат Новый ОписаниеОповещения(ИмяПроцедуры, Модуль, Новый Структура);
	
КонецФункции 

Функция ПостроитьКонвейернуюЛинию(Задания, ОбработчикОшибок, СледующийОбработчикЗадания)
	
	ОбработчикиЗаданий = Новый Массив(Задания.Количество() + 1); // + ОстановитьКонвейер
	
	ИндексЗадания = Задания.Количество();
	
	_ВыполнитьЗаданиеОстановитьКонвейер = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеОстановитьКонвейер",
		ЭтотОбъект,
		СледующийОбработчикЗадания
	);
	
	ОбработчикЗадания = ОбработчикЗадания(_ВыполнитьЗаданиеОстановитьКонвейер, Неопределено, ОбработчикОшибок);
	ОбработчикиЗаданий[ИндексЗадания] = ОбработчикЗадания;
	
	Пока ИндексЗадания > 0 Цикл
		
		ИндексЗадания = ИндексЗадания - 1;
		Задание = Задания[ИндексЗадания];
		ПараметрыЗадания = Задание.ДополнительныеПараметры;
		
		ОбработчикОшибокЗадания = Неопределено;
		Если ПараметрыЗадания.Property("ОбработчикОшибок", ОбработчикОшибокЗадания)
			И ОбработчикОшибокЗадания <> Неопределено Тогда
			ОбработчикОшибокЗадания.ДополнительныеПараметры.Вставить("ОбработчикиЗаданий", ОбработчикиЗаданий);
		КонецЕсли;  
		
		ОбработчикЗадания = ОбработчикЗадания(Задание, ОбработчикЗадания, ОбработчикОшибок);
		ОбработчикиЗаданий[ИндексЗадания] = ОбработчикЗадания;
		
	КонецЦикла; 
		
	ОбработчикОшибок.ДополнительныеПараметры.Вставить("ОбработчикиЗаданий", ОбработчикиЗаданий);
	
	Возврат ОбработчикЗадания;
	
КонецФункции 

#Область ПроизвольныеЭлементыКонвейера

// Произвольное задание. По окончании управление сразу передается на следующий этап в конвейере.
// ИмяПроцедуры - имя процедуры, которая должна быть экспортной, на клиенте, и иметь два параметра: КонтекстЗадания, ДополнительныеПараметры
// Модуль - модуль, в котором находится процедура
// ДополнительныеПараметры - произвольный параметр, который передается этапу в ДополнительныеПараметры
Функция ПроизвольноеЗадание(ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Задание = Новый ОписаниеОповещения(
		ИмяПроцедуры,
		Модуль,
		ДополнительныеПараметры
	);
	
	ПараметрыДекоратора = Новый Структура;
	ПараметрыДекоратора.Вставить("Задание", Задание);
	
	_ВыполнитьЗадание = Новый ОписаниеОповещения(
		"_ВыполнитьЗадание",
		ЭтотОбъект,
		ПараметрыДекоратора
	);	
	
	Возврат _ВыполнитьЗадание;
	
КонецФункции  

// Произвольный обработчик с одним параметром. По окончании управление сразу передается на следующий этап в конвейере.
// ИмяПроцедуры - имя процедуры, которая должна быть экспортной, на клиенте, и иметь один параметр: ДополнительныеПараметры.
// Модуль - модуль, в котором находится процедура.
// ДополнительныеПараметры - произвольный параметр, который передается обработчику в ДополнительныеПараметры.
Функция ПроизвольныйОбработчик1(ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Обработчик = Новый ОписаниеОповещения(
		ИмяПроцедуры,
		Модуль,
		ДополнительныеПараметры
	);
	
	ПараметрыДекоратора = Новый Структура;
	ПараметрыДекоратора.Вставить("Обработчик", Обработчик);
	
	_ВыполнитьОбработчик1 = Новый ОписаниеОповещения(
		"_ВыполнитьОбработчик1",
		ЭтотОбъект,
		ПараметрыДекоратора,
		"_ОбработатьОшибку",
		ЭтотОбъект
	);	
	
	Возврат _ВыполнитьОбработчик1;
	
КонецФункции

// Произвольный обработчик с двумя параметрами. По окончании управление сразу передается на следующий этап в конвейере.
// ИмяПроцедуры - имя процедуры, которая должна быть экспортной, на клиенте, и иметь два параметра: Результат, ДополнительныеПараметры.
// Модуль - модуль, в котором находится процедура.
// ДополнительныеПараметры - произвольный параметр, который передается обработчику в ДополнительныеПараметры.
Функция ПроизвольныйОбработчик2(ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Обработчик = Новый ОписаниеОповещения(
		ИмяПроцедуры,
		Модуль,
		ДополнительныеПараметры
	);
	
	ПараметрыДекоратора = Новый Структура;
	ПараметрыДекоратора.Вставить("Обработчик", Обработчик);
	
	_ВыполнитьОбработчик2 = Новый ОписаниеОповещения(
		"_ВыполнитьОбработчик2",
		ЭтотОбъект,
		ПараметрыДекоратора,
		"_ОбработатьОшибку",
		ЭтотОбъект
	);	
	
	Возврат _ВыполнитьОбработчик2;
	
КонецФункции

#КонецОбласти // ПроизвольныеЭлементыКонвейера

#Область Этапы

Функция ЭтапПроизвольноеЗадание(Линия, ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Линия.Задания.Добавить(ПроизвольноеЗадание(ИмяПроцедуры, Модуль, ДополнительныеПараметры));
	
	Возврат ЭтотОбъект;
	
КонецФункции 
	
Функция ЭтапЗапуститьВспомогательнуюЛинию(Линия, ВспомогательнаяЛиния, ОбработчикОшибок = Неопределено) Экспорт
	
	Линия.Задания.Добавить(ЗаданиеЗапуститьВспомогательнуюЛинию(ВспомогательнаяЛиния, ОбработчикОшибок));
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция ЭтапПоказатьДиалогВыбораФайла(Линия, Обработчик, ДиалогВыбораФайла, ОбработчикОшибок = Неопределено) Экспорт
	
	Линия.Задания.Добавить(ЗаданиеПоказатьДиалогВыбораФайла(Обработчик, ДиалогВыбораФайла, ОбработчикОшибок));	
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция ЭтапНачатьСозданиеКаталога(Линия, Обработчик, ИмяКаталога, ОбработчикОшибок = Неопределено) Экспорт
	
	Линия.Задания.Добавить(ЗаданиеНачатьСозданиеКаталога(Обработчик, ИмяКаталога, ОбработчикОшибок));
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция ЭтапНачатьУдалениеФайлов(Линия, Обработчик, Путь, Маска = Неопределено, ОбработчикОшибок = Неопределено) Экспорт
	
	Линия.Задания.Добавить(ЗаданиеНачатьУдалениеФайлов(Обработчик, Путь, Маска, ОбработчикОшибок));
	
	Возврат ЭтотОбъект;
	
КонецФункции 

#КонецОбласти // Этапы

#Область СтандартныеЗадания

Функция ЗаданиеЗапуститьВспомогательнуюЛинию(ВспомогательнаяЛиния, ОбработчикОшибок = Неопределено) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ВспомогательнаяЛиния", ВспомогательнаяЛиния);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	_ВыполнитьЗаданиеЗапуститьВспомогательнуюЛинию = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеЗапуститьВспомогательнуюЛинию",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	Возврат _ВыполнитьЗаданиеЗапуститьВспомогательнуюЛинию;
	
КонецФункции

Функция ЗаданиеПоказатьДиалогВыбораФайла(Обработчик, ДиалогВыбораФайла, ОбработчикОшибок = Неопределено) Экспорт
	
	Если Обработчик = Неопределено Тогда

		ПараметрыДекоратора = Новый Структура;
		ПараметрыДекоратора.Вставить("Обработчик", ОписаниеЗаглушки());

		Обработчик = Новый ОписаниеОповещения(
			"_ВыполнитьОбработчик2",
			ЭтотОбъект,
			ПараметрыДекоратора,
			"_ОбработатьОшибку",
			ЭтотОбъект
		);	
		
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Обработчик", Обработчик);
	ПараметрыЗадания.Вставить("ДиалогВыбораФайла", ДиалогВыбораФайла);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	_ВыполнитьЗаданиеПоказатьДиалогВыбораФайла = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеПоказатьДиалогВыбораФайла",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	Возврат _ВыполнитьЗаданиеПоказатьДиалогВыбораФайла;
	
КонецФункции 

Функция ЗаданиеНачатьСозданиеКаталога(Обработчик, ИмяКаталога, ОбработчикОшибок = Неопределено) Экспорт
	
	Если Обработчик = Неопределено Тогда

		ПараметрыДекоратора = Новый Структура;
		ПараметрыДекоратора.Вставить("Обработчик", ОписаниеЗаглушки());

		Обработчик = Новый ОписаниеОповещения(
			"_ВыполнитьОбработчик2",
			ЭтотОбъект,
			ПараметрыДекоратора,
			"_ОбработатьОшибку",
			ЭтотОбъект
		);	
		
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Обработчик", Обработчик);
	ПараметрыЗадания.Вставить("ИмяКаталога", ИмяКаталога);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	_ВыполнитьЗаданиеНачатьСозданиеКаталога = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеНачатьСозданиеКаталога",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	Возврат _ВыполнитьЗаданиеНачатьСозданиеКаталога;
	
КонецФункции 

Функция ЗаданиеНачатьУдалениеФайлов(Обработчик, Путь, Маска = Неопределено, ОбработчикОшибок = Неопределено) Экспорт
	
	Если Обработчик = Неопределено Тогда

		ПараметрыДекоратора = Новый Структура;
		ПараметрыДекоратора.Вставить("Обработчик", ОписаниеЗаглушки());

		Обработчик = Новый ОписаниеОповещения(
			"_ВыполнитьОбработчик1",
			ЭтотОбъект,
			ПараметрыДекоратора
		);	
		
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Обработчик", Обработчик);
	ПараметрыЗадания.Вставить("Путь", Путь);
	ПараметрыЗадания.Вставить("Маска", Маска);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	_ВыполнитьЗаданиеНачатьУдалениеФайлов = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеНачатьУдалениеФайлов",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	Возврат _ВыполнитьЗаданиеНачатьУдалениеФайлов;
	
КонецФункции

#КонецОбласти // СтандартныеЗадания

#Область РеализацияСтандартныхЗаданий

Процедура _ВыполнитьЗаданиеЗапуститьВспомогательнуюЛинию(КонтекстЗадания, ПараметрыЗадания) Экспорт
	
	Пуск(
		ПараметрыЗадания.ВспомогательнаяЛиния,
		ПараметрыЗадания.ОбработчикОшибок,
		КонтекстЗадания.СледующийОбработчикЗадания,
		КонтекстЗадания.ОбщиеПараметры,
		КонтекстЗадания.ИмяВызывающейПроцедуры
	);
	
КонецПроцедуры

Процедура _ВыполнитьЗаданиеПоказатьДиалогВыбораФайла(КонтекстЗадания, ПараметрыЗадания) Экспорт
	
	ПодготовитьПараметрыОбработчика(КонтекстЗадания, ПараметрыЗадания);
	
	_ПоказатьДиалогВыбораФайла = Новый ОписаниеОповещения(
		"_ПоказатьДиалогВыбораФайла",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	//НачатьПослеПодключенияРасширенияРаботыСФайлами(_ПоказатьДиалогВыбораФайла);
	
	ВыполнитьОбработкуОповещения(_ПоказатьДиалогВыбораФайла, Неопределено);
	
КонецПроцедуры

Процедура _ВыполнитьЗаданиеНачатьСозданиеКаталога(КонтекстЗадания, ПараметрыЗадания) Экспорт
	
	ПодготовитьПараметрыОбработчика(КонтекстЗадания, ПараметрыЗадания);
		
	_НачатьСозданиеКаталога = Новый ОписаниеОповещения(
		"_НачатьСозданиеКаталога",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	//НачатьПослеПодключенияРасширенияРаботыСФайлами(_НачатьСозданиеКаталога);
	
	ВыполнитьОбработкуОповещения(_НачатьСозданиеКаталога, Неопределено);
	
КонецПроцедуры 

Процедура _ВыполнитьЗаданиеНачатьУдалениеФайлов(КонтекстЗадания, ПараметрыЗадания) Экспорт
	
	ПодготовитьПараметрыОбработчика(КонтекстЗадания, ПараметрыЗадания);
		
	_НачатьУдалениеФайлов = Новый ОписаниеОповещения(
		"_НачатьУдалениеФайлов",
		ЭтотОбъект,
		ПараметрыЗадания
	);
	
	//НачатьПослеПодключенияРасширенияРаботыСФайлами(_НачатьУдалениеФайлов);
	
	ВыполнитьОбработкуОповещения(_НачатьУдалениеФайлов, Неопределено);
	
КонецПроцедуры

Процедура _ВыполнитьЗаданиеОстановитьКонвейер(КонтекстЗадания, СледующийОбработчикЗадания) Экспорт
	
	Инвариант(КонтекстЗадания.СледующийОбработчикЗадания = Неопределено); 
	
	Вызвать(
		СледующийОбработчикЗадания,
		КонтекстЗадания.ОбщиеПараметры,
		ИмяМодуля() + "._ВыполнитьЗаданиеОстановитьКонвейер()"
	);
	
КонецПроцедуры

#КонецОбласти // РеализацияСтандартныхЗаданий

#Область ОбработчикиЗаглушки

// Заглушки, которые используются в стандартных этапах в качестве обработчика если он не указан.

// Обработчик-заглушка с двумя параметрами

Функция ОписаниеЗаглушки()
	
	Возврат Новый ОписаниеОповещения("_ОбработчикЗаглушка", ЭтотОбъект);
	
КонецФункции

Процедура _ОбработчикЗаглушка(Result, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

#КонецОбласти // ОбработчикиЗаглушки

#Область Обертки

// Простые обертки, чтобы иметь возможность делать вызов через ВыполнитьОбработкуОповещения()


Процедура _ПоказатьДиалогВыбораФайла(Ничего, ДополнительныеПараметры) Экспорт
		
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	ДиалогВыбораФайла.Показать(ДополнительныеПараметры.Обработчик);	
	
КонецПроцедуры

Процедура _НачатьУдалениеФайлов(Ничего, ДополнительныеПараметры) Экспорт
		
	НачатьУдалениеФайлов(
		ДополнительныеПараметры.Обработчик,
		ДополнительныеПараметры.Путь,
		ДополнительныеПараметры.Маска
	);	
	
КонецПроцедуры

Процедура _НачатьСозданиеКаталога(Ничего, ДополнительныеПараметры) Экспорт
		
	НачатьСозданиеКаталога(
		ДополнительныеПараметры.Обработчик,
		ДополнительныеПараметры.ИмяКаталога
	);	
	
КонецПроцедуры

#КонецОбласти // Обертки

#Область Декораторы

// Декораторы, которые расширяют логику пользовательских процедур.
// Например, добавляют в конце передачу управления на следующий этап конвейера.


Процедура _ВыполнитьЗадание(Контекст, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Задание, Контекст);
	
	Вызвать(
		Контекст.СледующийОбработчикЗадания,
		Контекст.ОбщиеПараметры,
		СтрШаблон("%1._ВыполнитьЗадание[%2]()", ИмяМодуля(), ДополнительныеПараметры.Задание.ИмяПроцедуры)
	);
	
КонецПроцедуры 

Процедура _ВыполнитьОбработчик1(ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Обработчик);
	
	Вызвать(
		ДополнительныеПараметры._СледующийОбработчикЗадания,
		ДополнительныеПараметры._ОбщиеПараметры,
		СтрШаблон("%1._ВыполнитьОбработчик1[%2]()", ИмяМодуля(), ДополнительныеПараметры.Обработчик.ИмяПроцедуры)
	);
	
КонецПроцедуры

Процедура _ВыполнитьОбработчик2(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Обработчик, Результат);
	
	Вызвать(
		ДополнительныеПараметры._СледующийОбработчикЗадания,
		ДополнительныеПараметры._ОбщиеПараметры,
		СтрШаблон("%1._ВыполнитьОбработчик2[%2]()", ИмяМодуля(), ДополнительныеПараметры.Обработчик.ИмяПроцедуры)
	);
	
КонецПроцедуры

#КонецОбласти // Декораторы

#Область СлужебныеМетоды

Процедура ПодготовитьПараметрыОбработчика(КонтекстЗадания, ПараметрыЗадания)
	
	ПараметрыОбработчика = ПараметрыЗадания.Обработчик.ДополнительныеПараметры;
	
	Инвариант(ПараметрыОбработчика <> Неопределено);	
	
	// Для передачи управления на следующее задание в обработчиках нужна дополнительная информация.
	// В заданиях такой проблемы нет, так как им передается контекст, содержащий эту информацию.
	// Например в _ВыполнитьЗаданиеПоказатьДиалогВыбораФайла() будет подготовлена информация для передачи в _ВыполнитьОбработчик2(). 
	ПараметрыОбработчика.Вставить("_СледующийОбработчикЗадания", КонтекстЗадания.СледующийОбработчикЗадания);
	ПараметрыОбработчика.Вставить("_ОбщиеПараметры", КонтекстЗадания.ОбщиеПараметры);
	
	// При обработке системной ошибки нужно иметь возможность вызвать указанный для задания произвольный обработчик ошибок.
	ПараметрыОбработчика.Вставить("_ОбработчикОшибок", ПараметрыЗадания.ОбработчикОшибок); // см. например ОбработатьОшибку()
	
КонецПроцедуры

Функция ИмяМодуля()
	
	Возврат "КонвейерЗаданийКлиент";
	
КонецФункции

Процедура Инвариант(Условие, ТекстИсключения = Неопределено)
	
	Если Не Условие Тогда
		Если ТекстИсключения = Неопределено Тогда
			ТекстИсключения = НСтр(
				"ru = 'нарушение протокола';
				|en = 'violation of protocol'"
			);
		КонецЕсли; 
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецПроцедуры 	
	
#КонецОбласти // СлужебныеМетоды
