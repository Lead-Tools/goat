
// Copyright 2019 Tsukanov Alexander. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

Функция СоздатьЛинию() Экспорт
	
	Линия = Новый Структура;
	Линия.Вставить("Задания", Новый Массив);
	Линия.Вставить("ОбработчикПервогоЗадания", Неопределено);
	
	Возврат Линия;
	
КонецФункции 

Процедура НачатьВыполнение(Линия, ОбработчикОшибок, СледующийОбработчикЗадания, ОбщиеПараметры, ИмяВызывающейПроцедуры) Экспорт
	
	Линия.ОбработчикПервогоЗадания = КонвейерЗаданийСлужебныйКлиент.ПостроитьКонвейернуюЛинию(
		Линия.Задания,
		ОбработчикОшибок,
		СледующийОбработчикЗадания
	);
	
	КонвейерЗаданийСлужебныйКлиент.Вызвать(
		Линия.ОбработчикПервогоЗадания,
		ОбщиеПараметры,
		ИмяВызывающейПроцедуры
	);
	
КонецПроцедуры

#Область КонструкторыЗаданий

// Произвольное задание. По окончании управление сразу передается на следующий этап в конвейере.
// ИмяПроцедуры - имя процедуры, которая должна быть экспортной, на клиенте, и иметь два параметра: КонтекстЗадания, ДополнительныеПараметры
// Модуль - модуль, в котором находится процедура
// ДополнительныеПараметры - произвольный параметр, который передается этапу в ДополнительныеПараметры
Функция ПроизвольноеЗадание(Линия, ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	Обработчик = Новый ОписаниеОповещения(
		ИмяПроцедуры,
		Модуль,
		ДополнительныеПараметры
	);
	
	ДекорированныйОбработчик = КонвейерЗаданийСлужебныйКлиент.ДекорироватьОбработчик(Обработчик);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ДекорированныйОбработчик", ДекорированныйОбработчик);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьПроизвольноеЗадание = Новый ОписаниеОповещения(
		"_ВыполнитьПроизвольноеЗадание",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);	
	
	Линия.Задания.Добавить(_ВыполнитьПроизвольноеЗадание);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция НачалоУсловногоБлока(Линия, ИмяПроцедуры, Модуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	// без декоратора
	Обработчик = Новый ОписаниеОповещения(
		ИмяПроцедуры,
		Модуль,
		ДополнительныеПараметры
	);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Обработчик", Обработчик);
	ПараметрыЗадания.Вставить("ОбработчикЗаданияОкончаниеУсловногоБлока", Неопределено);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеНачалоУсловногоБлока = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеНачалоУсловногоБлока",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
		
	Линия.Задания.Добавить(_ВыполнитьЗаданиеНачалоУсловногоБлока);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ОкончаниеУсловногоБлока(Линия) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеОкончаниеУсловногоБлока = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеОкончаниеУсловногоБлока",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеОкончаниеУсловногоБлока);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция НачалоПопытки(Линия) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеНачалоПопытки = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеНачалоПопытки",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеНачалоПопытки);
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция НачалоИсключения(Линия) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеНачалоИсключения = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеНачалоИсключения",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеНачалоИсключения);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ОкончаниеПопытки(Линия) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеОкончаниеПопытки = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеОкончаниеПопытки",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеОкончаниеПопытки);
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция ДиалогВыбораФайла(Линия, Обработчик, ДиалогВыбораФайла, ОбработчикОшибок = Неопределено) Экспорт
	
	ДекорированныйОбработчик = КонвейерЗаданийСлужебныйКлиент.ДекорироватьОбработчик(Обработчик);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ДекорированныйОбработчик", ДекорированныйОбработчик);
	ПараметрыЗадания.Вставить("ДиалогВыбораФайла", ДиалогВыбораФайла);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеДиалогВыбораФайла = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеДиалогВыбораФайла",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеДиалогВыбораФайла);
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция СозданиеКаталога(Линия, Обработчик, ИмяКаталога, ОбработчикОшибок = Неопределено) Экспорт
	
	ДекорированныйОбработчик = КонвейерЗаданийСлужебныйКлиент.ДекорироватьОбработчик(Обработчик);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ДекорированныйОбработчик", ДекорированныйОбработчик);
	ПараметрыЗадания.Вставить("ИмяКаталога", ИмяКаталога);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеСозданиеКаталога = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеСозданиеКаталога",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеСозданиеКаталога);
	
	Возврат ЭтотОбъект;
	
КонецФункции 

Функция УдалениеФайлов(Линия, Обработчик, Путь, Маска = Неопределено, ОбработчикОшибок = Неопределено) Экспорт
	
	ДекорированныйОбработчик = КонвейерЗаданийСлужебныйКлиент.ДекорироватьОбработчик(Обработчик, "_ВыполнитьОбработчик1");
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ДекорированныйОбработчик", ДекорированныйОбработчик);
	ПараметрыЗадания.Вставить("Путь", Путь);
	ПараметрыЗадания.Вставить("Маска", Маска);
	ПараметрыЗадания.Вставить("ОбработчикОшибок", ОбработчикОшибок);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеУдалениеФайлов = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеУдалениеФайлов",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеУдалениеФайлов);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ДиалогВопроса(Линия, Обработчик, ТекстВопроса, Кнопки, Таймаут = 0, КнопкаПоУмолчанию = Неопределено, Заголовок = Неопределено, КнопкаТаймаута = Неопределено) Экспорт
	
	ДекорированныйОбработчик = КонвейерЗаданийСлужебныйКлиент.ДекорироватьОбработчик(Обработчик);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ДекорированныйОбработчик", ДекорированныйОбработчик);
	ПараметрыЗадания.Вставить("ТекстВопроса", ТекстВопроса);
	ПараметрыЗадания.Вставить("Кнопки", Кнопки);
	ПараметрыЗадания.Вставить("Таймаут", Таймаут);
	ПараметрыЗадания.Вставить("КнопкаПоУмолчанию", КнопкаПоУмолчанию);
	ПараметрыЗадания.Вставить("Заголовок", Заголовок);
	ПараметрыЗадания.Вставить("КнопкаТаймаута", КнопкаТаймаута);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеДиалогВопроса = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеДиалогВопроса",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеДиалогВопроса);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция ОстановкаКонвейера(Линия) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("СледующийОбработчикЗадания", Неопределено);
	
	ДополнитьПараметрыЗадания(ПараметрыЗадания);
	
	_ВыполнитьЗаданиеОстановкаКонвейера = Новый ОписаниеОповещения(
		"_ВыполнитьЗаданиеОстановкаКонвейера",
		КонвейерЗаданийСлужебныйКлиент,
		ПараметрыЗадания
	);
	
	Линия.Задания.Добавить(_ВыполнитьЗаданиеОстановкаКонвейера);
	
	Возврат ЭтотОбъект;
	
КонецФункции 

#КонецОбласти // КонструкторыЗаданий

#Область СлужебныеМетоды

// Создает новый обработчик ошибок
Функция ОбработчикОшибок(ИмяПроцедуры, Модуль) Экспорт
	
	// TODO: может лучше декорировать?
	
	Возврат Новый ОписаниеОповещения(ИмяПроцедуры, Модуль, Новый Структура);
	
КонецФункции

Функция ИзвлечьЗначение(Значение) Экспорт
	
	Возврат КонвейерЗаданийСлужебныйКлиент.ИзвлечьЗначение(Значение); 
	
КонецФункции 

Функция Ссылка(Коллекция, Ключ) Экспорт
	
	Возврат КонвейерЗаданийСлужебныйКлиент.Ссылка(Коллекция, Ключ);
	
КонецФункции 

Функция ДополнитьПараметрыЗадания(ПараметрыЗадания)
	
	ДобавитьСвойствоЕслиОтсутствует(ПараметрыЗадания, "ОбработчикОшибок");
	ДобавитьСвойствоЕслиОтсутствует(ПараметрыЗадания, "ОбработчикЗаданияНачалоИсключения");
	
КонецФункции 

Процедура ДобавитьСвойствоЕслиОтсутствует(Структура, ИмяСвойства)
	
	Если Не Структура.Свойство(ИмяСвойства) Тогда
		Структура.Вставить(ИмяСвойства);
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти // СлужебныеМетоды
