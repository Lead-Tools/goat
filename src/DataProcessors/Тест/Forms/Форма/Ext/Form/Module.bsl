
&НаКлиенте
Процедура ТестСинхронный(Command)
	
	ОчиститьСообщения();
	
	ДиалогВыбораПапки = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораПапки.МножественныйВыбор = Ложь;
	ДиалогВыбораПапки.Заголовок = НСтр("ru = 'Выберите каталог'; en = 'Select the folder'");
	
	Если ДиалогВыбораПапки.Выбрать() Тогда
		
		ИмяПапки = ДиалогВыбораПапки.Каталог;
				
		Попытка
			
			СоздатьКаталог(ИмяПапки);
			СоздатьКаталог(ИмяПапки + "\_проверка_доступа_\");
			УдалитьФайлы(ИмяПапки + "\_проверка_доступа_\");
			
			Сообщить(ИмяПапки);
			Сообщить("Успех");
			
		Исключение
			
			// нет прав скорее всего
			
			Сообщить("Ошибка");
			
			Возврат;
			
		КонецПопытки;
		
		Сообщить("Хвост1");
		
	КонецЕсли;
	
	Сообщить("Хвост2");
	
КонецПроцедуры

&НаКлиенте
Процедура ТестАсинхронный(Команда)
	
	ОчиститьСообщения();
	
	Конвейер = КонвейерЗаданийКлиент; 
	
	ДиалогВыбораПапки = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораПапки.МножественныйВыбор = Ложь;
	ДиалогВыбораПапки.Заголовок = НСтр("ru = 'Выберите каталог'; en = 'Select the folder'");
	
	// Ящик, который двигается по конвейеру через все этапы (см. ОбщиеПараметры в обработчиках заданий)
	ОбщиеПараметры = Новый Структура;
	ОбщиеПараметры.Вставить("ИмяПапки", Неопределено);
	ОбщиеПараметры.Вставить("ИмяПодпапки", Неопределено);
	
	СсылкаНаИмяПапки = Конвейер.Ссылка(ОбщиеПараметры, "ИмяПапки");
	СсылкаНаИмяПодпапки = Конвейер.Ссылка(ОбщиеПараметры, "ИмяПодпапки");
	
	// Обработчик ошибок, возникших на конвейере
	ОбработчикОшибок = Конвейер.ОбработчикОшибок("ОбработчикОшибок", ЭтотОбъект);
		
	// Формирование этапов конвейера и запуск
	
	Линия = Конвейер.СоздатьЛинию();
	
	#Область Конвейер	
	
	Конвейер.ДиалогВыбораФайла(Линия, Обработчик("ОбработатьВыборПапки", ОбщиеПараметры), ДиалогВыбораПапки);
	Конвейер.Оператор_Если(Линия, "ПроверкаУсловия", ЭтотОбъект);
		Конвейер.Оператор_Попытка(Линия);
			Конвейер.СозданиеКаталога(Линия, Неопределено, СсылкаНаИмяПапки);
			Конвейер.СозданиеКаталога(Линия, Неопределено, СсылкаНаИмяПодпапки, ОбработчикОшибок);
			Конвейер.УдалениеФайлов(Линия, Неопределено, СсылкаНаИмяПодпапки);
			Конвейер.ПроизвольноеЗадание(Линия, "ЗаданиеПриУспехе", ЭтотОбъект, Новый Структура("СсылкаНаИмяПапки", СсылкаНаИмяПапки));
		Конвейер.Оператор_Исключение(Линия);
			Конвейер.ПроизвольноеЗадание(Линия, "ЗаданиеОшибка", ЭтотОбъект);
			Конвейер.Оператор_Возврат(Линия);
		Конвейер.Оператор_КонецПопытки(Линия);
		Конвейер.ПроизвольноеЗадание(Линия, "ЗаданиеХвост1", ЭтотОбъект);
	Конвейер.Оператор_КонецЕсли(Линия);		
	Конвейер.ПроизвольноеЗадание(Линия, "ЗаданиеХвост2", ЭтотОбъект);
	Конвейер.НачатьВыполнение(Линия, ОбщиеПараметры, ОбработчикОшибок);
	
	#КонецОбласти // Конвейер
		
КонецПроцедуры

&НаКлиенте
Функция Обработчик(ИмяПроцедуры, ПараметрыОбработчика)
	Возврат Новый ОписаниеОповещения(ИмяПроцедуры, ЭтотОбъект, ПараметрыОбработчика)
КонецФункции 	
	
&НаКлиенте
Функция ПроверкаУсловия(ОбщиеПараметры, ДополнительныеПараметры) Экспорт
	
	Возврат ОбщиеПараметры.ИмяПапки <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборПапки(Результат, ОбщиеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;		
	КонецЕсли;
	
	ИмяПапки = Результат[0];
	
	ОбщиеПараметры.ИмяПапки = ИмяПапки;
	ОбщиеПараметры.ИмяПодпапки = ИмяПапки + "\_проверка_доступа_\";
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеПриУспехе(ОбщиеПараметры, ПараметрыЗадания) Экспорт
	
	Сообщить(КонвейерЗаданийКлиент.ИзвлечьЗначение(ПараметрыЗадания.СсылкаНаИмяПапки));
	Сообщить("Успех");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеОшибка(ОбщиеПараметры, ПараметрыЗадания) Экспорт
	
	Сообщить("Ошибка");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеХвост1(ОбщиеПараметры, ПараметрыЗадания) Экспорт
	
	Сообщить("Хвост1");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеХвост2(ОбщиеПараметры, ПараметрыЗадания) Экспорт
	
	Сообщить("Хвост2");
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОшибок(КонтекстОшибки, ДополнительныеПараметры) Экспорт
	
	// нет прав скорее всего

	Сообщить(СтрШаблон("Ошибка: Текст = ""%1""; ИмяПапки = ""%2""", КраткоеПредставлениеОшибки(КонтекстОшибки.ИнформацияОбОшибке), КонтекстОшибки.ОбщиеПараметры.ИмяПапки));
		
КонецПроцедуры
